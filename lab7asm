			.title	"morse.asm"
			.def     main_asm
			.def     doSpace
			.def     doDot
			.def     doDash
			.def     interSpace
			.def     initialize
			.def     beep_cnt
			.def     delay_cnt;
			.def     second_cnt;
			.def     beep_bool;
			.def     debounce_cnt;

;*******************************************************************************
;     Project:  morse.asm (02/15/2017)
;      Author:  Maura Walker, Brigham Young University
;
; Description:  Outputs a message in Morse Code using a LED and a transducer
;               (speaker).  The watchdog is configured as an interval timer.
;               The watchdog interrupt service routine (ISR) toggles the green
;               LED every second and pulse width modulates (PWM) the speaker
;               such that a tone is produced.
;  Disclaimer:  This code is the work of Maura Walker.  I certify this to be my
;                 source code and not obtained from any student, past or current.
;
;	Morse code is composed of dashes and dots:
;
;        1. A dot is equal to an element of time.
;        2. One dash is equal to three dots.
;        3. The space between parts of the letter is equal to one dot.
;        4. The space between two letters is equal to three dots.
;        5. The space between two words is equal to seven dots.
;
;    5 WPM = 60 sec / (5 * 50) elements = 240 milliseconds per element.
;    element = (WDT_IPS * 6 / WPM) / 5
;
;******************************************************************************
; Light instruction shortcuts-------------------------------------------------
	        .asg   "bis.b #0x40,&P4OUT",RED_ON
	        .asg   "bic.b #0x40,&P4OUT",RED_OFF
	        .asg   "xor.b #0x40,&P4OUT",RED_TOGGLE
	        .asg   "bit.b #0x40,&P4OUT",RED_TEST

	        .asg   "bis.b #0x10,&P3OUT",GREEN_ON
	        .asg   "bic.b #0x10,&P3OUT",GREEN_OFF
	        .asg   "xor.b #0x10,&P3OUT",GREEN_TOGGLE
	        .asg   "bit.b #0x10,&P3OUT",GREEN_TEST

	        .asg   "xor.b #0x02,&P4OUT",ORANGE_TOGGLE

; System equates --------------------------------------------------------------
            .cdecls C,"msp430.h"
			.cdecls C,"morse.h"
 ;           .def    RESET                   ; Export program entry-point to linker


; External references ---------------------------------------------------------
            .ref    numbers                 ; codes for 0-9
            .ref    letters                 ; codes for A-Z
                            
; Global variables ------------------------------------------------------------
            .bss    beep_cnt,2              ; beeper flag
            .bss    delay_cnt,2             ; delay flag
            .bss    second_cnt, 2
            .bss    debounce_cnt,2          ; debounce count
            .bss    switches, 2
            .bss    beep_bool, 1

; Program section -------------------------------------------------------------
            .text                           ; program section
message:    .string "HELLO CS 224 WORLD "
            .byte   0
            .align  2                       ; align on word boundary

; power-up reset --------------------------------------------------------------
;RESET:      ;mov.w   #STACK,SP               ; initialize stack pointer
initialize:	GREEN_OFF
			RED_OFF
			bic.b   #0x01,&P1SEL           ; select GPIO
            bic.b   #0x01,&P1DIR           ; configure P1.0 as inputs
            bis.b   #0x01,&P1OUT           ; use pull-up
            bis.b   #0x01,&P1REN           ; enable pull-up
            bis.b   #0x01,&P1IES           ; trigger on high to low transition
            bis.b   #0x01,&P1IE            ; P1.0 interrupt enabled
            bic.b   #0x01,&P1IFG           ; P1.0 IFG cleared

            bis.w   #GIE,SR                  ; enable general interrupts

			bis.b   #0x20,&P4DIR			; set P4.5 as output (speaker)
			bis.b   #0x40,&P4DIR            ; set P4.0-3,6 as output
			bis.b   #0x10,&P3DIR            ; set P3.4 as output
			ret
            ;call    #main_asm               ; call main function
            ;jmp     $                       ; you should never get here!

; start main function vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv	
main_asm:   mov.w   #WDT_CTL,&WDTCTL        ; set WD timer interval
            mov.b   #WDTIE,&IE1             ; enable WDT interrupt
            clr.w   &beep_cnt               ; clear counters
            clr.w   &delay_cnt
            mov.w   #WDT_IPS, &second_cnt
            mov.w   #1, &beep_bool

loop:       mov.w   #message, r4
;loop:      call    #doDot
;           call    #doDash
;           call    #doSpace
;            jmp     loop

loop2:      mov.b   @r4+,r5
			cmp.b   #32, r5
			  jeq   loop6
			cmp.b   #58, r5
			  jl    number

letter:	    sub.w   #'A',r5
			add.w   r5,r5
  			mov.w   letters(r5),r5
            jmp     loop3

number:     sub.w   #'0',r5
			add.w   r5,r5
            mov.w   numbers(r5),r5

loop3:      mov.b   @r5+,r6
            cmp.b   #DOT,r6
              jeq   loop4
            cmp.b   #DASH, r6
              jeq   loop5
            cmp.b   #END, r6
              jeq   loop7
            jmp     loop
; end main function ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

loop4:      call    #doDot
            jmp     loop3

loop5:      call    #doDash
            jmp     loop3

loop6:      call    #doSpace
			jmp     loop2

loop7:      call    #interSpace
			jmp     loop2

doDot:      push.w  r15
            mov.w   #ELEMENT,r15            ; output DOT
            call    #beep
            mov.w   #ELEMENT,r15            ; delay 1 element
            call    #delay
            pop.w   r15
            ret

doDash:     push.w  r15
            mov.w   #ELEMENT*3,r15          ; output DASH
            call    #beep
            mov.w   #ELEMENT,r15            ; delay 1 element
            call    #delay
            pop.w   r15
            ret

interSpace: push.w  r15
            mov.w   #ELEMENT*2,r15          ; output space
            call    #delay                  ; delay
            pop.w   r15
            ret

doSpace:    push.w  r15
            mov.w   #ELEMENT*4,r15          ; output space
            call    #delay                  ; delay
            pop.w   r15
            ret


; beep (r15) ticks subroutine -------------------------------------------------
beep:       mov.w   r15,&beep_cnt           ; start beep

beep02:     tst.w   &beep_cnt               ; beep finished?
              jne   beep02                  ; n
            ret                             ; y


; delay (r15) ticks subroutine ------------------------------------------------
delay:      mov.w   r15,&delay_cnt          ; start delay

delay02:    tst.w   &delay_cnt              ; delay done?
              jne   delay02                 ; n
            ret                             ; y


; Watchdog Timer ISR ----------------------------------------------------------
WDT_ISR:    tst.w   debounce_cnt            ; debouncing?
              jeq   WDT_CONT                ; n
; debounce switches & process
            dec.w   debounce_cnt          ; y, decrement count, done?
              jne   WDT_CONT               ; n
            mov.b   &P1IN,switches             ; read switches
            and.b   #0x0f,switches
            xor.b   #0x0f,switches            ; any switches?
              jeq   WDT_CONT
            xor.b   #1,&beep_bool			; toggle whether the machine should beep or not

WDT_CONT:	tst.w   &beep_cnt               ; beep on?
              jeq   WDT_02                  ; n
            dec.w   &beep_cnt               ; y, decrement count
            RED_ON

            cmp.b   #1, &beep_bool
              jeq   WDT_BEEP
            jmp     WDT_02

WDT_BEEP:   xor.b   #0x20,&P4OUT			; beep using 50% PWM

WDT_02:		RED_OFF
			tst.w &second_cnt
			  jeq WDT_03
			dec.w &second_cnt
			jmp WDT_04

WDT_03:     GREEN_TOGGLE
			mov.w   #WDT_IPS, &second_cnt

WDT_04:		tst.w   &delay_cnt              ; delay?
              jeq   WDT_10                  ; n
            dec.w   &delay_cnt              ; y, decrement count

WDT_10:     reti                            ; return from interrupt

;P1_ISR:     bic.b  #0x01,&P1IFG             ; clear P1.0 Interrupt Flag
;            mov.w  #DEBOUNCE,debounce_cnt  ; reset debounce count
;            reti

; Interrupt Vectors -----------------------------------------------------------
            .sect   ".int10"                ; Watchdog Vector
            .word   WDT_ISR                 ; Watchdog ISR

            ;.sect  ".int02"              ; P1 interrupt vector
            ;.word  P1_ISR

            ;.sect   ".reset"                ; PUC Vector
            ;.word   RESET                   ; RESET ISR
            .end
